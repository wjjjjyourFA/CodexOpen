MY_SYSTEM_INFO = 20.04.1
unix{
 MY_SYSTEM_INFO = $$system(uname -a | cut -d \~ -f 2 | cut -d \- -f 1)
 message("System Is Ubuntu" $${QT_ARCH} : $${MY_SYSTEM_INFO})
}

TEMPLATE = app
CONFIG += console c++17
CONFIG -= app_bundle
CONFIG -= qt
# 启用 OpenMP 支持
CONFIG += openmp
TARGET = lidar_camera_fusion_test
DESTDIR = $$PWD/../../../../install/bin

CONFIG(debug, debug|release) {
  OBJECTS_DIR = ./../../../../build/Debug/$$TARGET
  QMAKE_CXXFLAGS += -O1
}
CONFIG(release, debug|release) {
  OBJECTS_DIR = ./../../../../build/Release/$$TARGET
  QMAKE_CXXFLAGS += -O3
}

SOURCES += \
  ./../../../../modules/perception/common/camera/parameter/*.cpp \
  ./../../../../tools/common/camera_undistortion/*.cpp \
  ./../../../../modules/perception/tools/common/show_data_3d.cpp \
  ./../../../../modules/perception/tools/common/show_data_2d.cpp \
  ./../../../../modules/perception/tools/common/colors.cpp \
  lidar_camera_fusion.cpp \
  lidar_camera_fusion_test.cpp \
  ./params/params.cpp \

HEADERS += \
  ./../../../../modules/perception/common/camera/parameter/*.h \
  ./../../../../tools/common/camera_undistortion/*.h \
  ./../../../../modules/perception/tools/common/show_data_3d.h \
  ./../../../../modules/perception/tools/common/show_data_2d.h \
  ./../../../../modules/perception/tools/common/colors.hpp \
  ./../../../../modules/perception/tools/save_file/save_ply.h \
  lidar_camera_fusion.h \
  ./params/params.h \


INCLUDEPATH += \
  /usr/include/eigen3 \
  /usr/include/opencv4 \
  /usr/local/include/opencv4 \
  /usr/local/include \
  ./../../../../

contains(MY_SYSTEM_INFO, 20.04.1){
INCLUDEPATH += \
    /usr/include/pcl-1.10 \
    /usr/include/vtk-7.1
LIBS += \
    -lvtkCommonCore-7.1 -lvtkCommonDataModel-7.1 -lvtkCommonMath-7.1 -lvtkFiltersCore-7.1 -lvtksys-7.1 -lvtkRenderingCore-7.1 -lvtkFiltersHybrid-7.1
    }
contains(MY_SYSTEM_INFO, 18.0){
INCLUDEPATH += \
    /usr/include/pcl-1.8 \
    /usr/include/vtk-6.3
LIBS += \
    -lvtkCommonCore-6.3 -lvtkCommonDataModel-6.3 -lvtkCommonMath-6.3 -lvtkFiltersCore-6.3 -lvtksys-6.3 -lvtkRenderingCore-6.3 -lvtkFiltersHybrid-6.3
    }

LIBS += \
  -L/usr/local/lib \
  -L/usr/lib/aarch64-linux-gnu \
  -L/usr/lib/x86_64-linux-gnu/ \
  -lopencv_core -lopencv_highgui -lopencv_imgproc -lopencv_imgcodecs \
  -lpcl_common -lpcl_filters -lpcl_io -lpcl_io_ply \
  -lpcl_visualization \
  -lgomp
